int val;
const byte mask = (1 << 5); // маска 00100000, соответствующая пину 13 (PortB5). Получается пятикратным сдвигом байта 0b00000001 влево.
                            // Эквивалентно byte mask = 0b00100000;

void setup()
{
 Serial.begin(9600);
 DDRB |= mask;   // пин 13 (PortB5) настроить как выход
}


void loop()
{
 val = Serial.read(); // получить байт из последовательного порта
 if (val == '1') PORTB |= mask; // если полученный байт соответствует символу '1', то установить PortB5 в лог. 1, что приведёт к зажиганию светодиода, подключённого к пину 13
 if (val == '0') PORTB &= ~mask; // если полученный байт соответствует символу '0', то сбросить PortB5, что приведёт к гашению светодиода
}

/*
Иллюстративная программа для управления встроенным светодиодом на плате Arduino UNO через последовательный порт.
Светодиод включается, если передан символ '1', выключается, если передан символ '0'.
При передаче других символов состояние светодиода не меняется.

Для управления светодиодом, подключённым к пину 13 Arduino:
- (шаг 0) однократно настраивают пин 13 на использование в качестве выхода;
- для зажигания светодиода выдают на пин 13 лог. 1 (шаг 1), для гашения лог. 0 (шаг 2).

Пину 13 соответствует бит 5 (счёт от нуля) в порте B.
Для выполнения шага 0 необходимо записать в бит 5 регистра DDRB единицу, не меняя остальные биты
(DDRB -- Data Direction Register, регистр направления порта B, служит для настройки пина как входа (0) или выхода (1)).
Для выполнения шагов 1 и 2 необходимо записать в бит 5 регистра PORTB единицу или ноль, не меняя остальные биты.

Для записи 1 или 0 в конкретный бит без изменения остальных битов используют следующую операцию:
- заранее создают маску: байт, в котором биту, подлежащему изменению, соответствует 1, а остальным битам 0.
  Например, для работы с битом 5 маской будет байт 00100000;
- считывают регистр, подлежащий изменению, в копию.
  Например, для выполнения щага 0 считывают регистр DDRB:
- побитно выполняют с маской и копией логическую операцию, зависящую от того, что требуется сделать.
  Конкретно, если бит, на который указывает маска, требуется 
  -- установить в 1, то используют операцию маска ИЛИ копия;
  -- установить в 0, то используют операцию (НЕ маска) И копия;
  -- инвертировать, то используют операцию маска XOR копия;
- записывают результат логической операции в регистр, подлежащий изменению.

В программе для создания маски, указывающей бит 5, берут число 1 (оно хранится как байт 00000001) 
и сдвигают его влево 5 раз (1 << 5): 00000001 << 5 = 00100000. Возможно и явное присваивание значения: mask = 0b00100000. 

Для настройки пина 5 для работы в качестве выхода считывают DDRB в копию и выполняют
DDRB |= mask; (это краткая запись, эквивалентная DDRB = DDRB | mask;)

Для зажигания светодиода в бит 5 записывают лог. 1: PORTB |= mask;
Для гашения светодиода в бит 5 записывают лог. 0:   PORTB &= ~mask;

ССЫЛКИ
1. Alex Gyver. Монитор порта. Serial. https://alexgyver.ru/lessons/serial/
2. Serial.Read с примером использования. https://arduino.ru/Reference/Serial/Read

 */